plugins {
    id 'org.springframework.boot' version '2.2.2.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'java-library'
}

group = 'de.waschndolos'
version = '0.0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

task cleanupOldFrontend(type: Delete) {
    delete "${project.projectDir.path}/src/main/resources/static"
}

task buildFrontend(type: Exec) {
    dependsOn cleanupOldFrontend
    workingDir 'frontend'
    commandLine 'cmd', '/c', 'ng build --prod'
}

task copyFrontend(type: Copy) {
    dependsOn buildFrontend
    from "${project.projectDir.path}/frontend/dist/frontend"
    into "${project.projectDir.path}/src/main/resources/static"
}

bootJar {
    dependsOn copyFrontend
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}